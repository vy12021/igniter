apply plugin: 'com.android.application'

ext.versionMajor = 0
ext.versionMinor = 11
ext.versionPatch = 0
ext.versionClassifier = "beta" // or null
ext.isSnapshot = true // set to false when publishing new releases
ext.minimumSdkVersion = 23
ext.targetSdkVersion = 35

private Integer GenerateVersionCode() {
    return ext.minimumSdkVersion * 10000000 + ext.versionMajor * 10000 + ext.versionMinor * 100 + ext.versionPatch
}

private String GenerateVersionName() {
    String versionName = "${ext.versionMajor}.${ext.versionMinor}.${ext.versionPatch}"
    if (ext.versionClassifier != null) {
        versionName += "-" + ext.versionClassifier
    }

    if (ext.isSnapshot) {
        versionName += "-" + "SNAPSHOT"
    }
    return versionName;
}

android {
    ndkVersion "25.1.8937393"
    compileSdk targetSdkVersion
    namespace "io.github.trojan_gfw.igniter"

    applicationVariants.all { variant ->
        variant.resValue "string", "versionName", variant.versionName
    }

    defaultConfig {
        applicationId "io.github.trojan_gfw.igniter"
        minSdk project.ext.minimumSdkVersion
        targetSdk project.ext.targetSdkVersion
        versionCode GenerateVersionCode()
        versionName GenerateVersionName()
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        ndk {
            abiFilters "armeabi-v7a", "arm64-v8a", "x86", "x86_64"
        }
        externalNativeBuild {
            cmake {
                arguments "-DANDROID_CPP_FEATURES=rtti exceptions"
            }
        }
        archivesBaseName = "$applicationId-v$versionName-$versionCode"
    }
    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            debuggable true
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'),
                    'proguard-rules.pro'
        }
    }
    externalNativeBuild {
        cmake {
            path file('src/main/cpp/CMakeLists.txt')
        }
    }
    buildFeatures {
        aidl true
        buildConfig true
    }
    compileOptions {
        // Flag to enable support for the new language APIs
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    packaging {
        jniLibs {
            // During installation, the installer decompresses the libraries, and the linker loads
            // the decompressed libraries at runtime; in this case, the APK would be smaller, but
            // installation time might be slightly longer.
            // We optimize for size to make users happy
            useLegacyPackaging = true
            keepDebugSymbols += ['*/armeabi-v7a/libgojni.so', '*/arm64-v8a/libgojni.so', '*/x86/libgojni.so', '*/x86_64/libgojni.so']
        }
    }
    splits {
        abi {
            enable true
            reset()
            include "armeabi-v7a", "arm64-v8a", "x86", "x86_64"
            universalApk true
        }
    }
}

repositories {
    flatDir {
        dirs 'src/libs'
    }
}

dependencies {
    implementation 'com.github.stealthcopter:AndroidNetworkTools:0.4.5.3'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.activity:activity:1.9.3'
    implementation 'androidx.fragment:fragment:1.8.5'
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.recyclerview:recyclerview:1.3.2'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.0'
    implementation 'androidx.core:core:1.15.0'
    implementation 'androidx.preference:preference:1.2.1'
    implementation 'com.google.code.gson:gson:2.11.0'
    // CameraX core library using camera2 implementation
    implementation 'androidx.camera:camera-camera2:1.4.0'
    // CameraX Lifecycle Library
    implementation 'androidx.camera:camera-lifecycle:1.4.0'
    // CameraX View class
    implementation 'androidx.camera:camera-view:1.4.0'
    implementation 'com.google.mlkit:barcode-scanning:17.3.0'

    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.1.2'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'

    api(name: 'golibs', ext: 'aar')
}
